/* 
 * AT-SPI - Assistive Technology Service Provider Interface 
 * (Gnome Accessibility Project; http://developer.gnome.org/projects/gap)
 *
 * Copyright 2001 Sun Microsystems Inc.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Library General Public
 * License as published by the Free Software Foundation; either
 * version 2 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Library General Public License for more details.
 *
 * You should have received a copy of the GNU Library General Public
 * License along with this library; if not, write to the
 * Free Software Foundation, Inc., 59 Temple Place - Suite 330,
 * Boston, MA 02111-1307, USA.
 */

#ifndef __ACCESSIBILITY_APPLICATION_DEFINED__
#define __ACCESSIBILITY_APPLICATION_DEFINED__

#include <Bonobo_Unknown.idl>
#include <Accessibility_Accessible.idl>

module Accessibility {
  interface Application : Accessible {

    /**
     * @toolkitName:
     * A string indicating the type of user interface toolkit
     * which is used by the application.
     * Ordinarily clients of @AccessibleApplication should be
     * toolkit-agnostic, dependencies on this method probably
     * should be reconsidered!
     **/
    readonly attribute string toolkitName;

    /**
     * @version:
     * A string indicating the version number of the application's
     * accessibility bridge implementation.
     **/
    readonly attribute string version;

    /**
     * @id:
     * The application instance's unique ID as assigned by the registry.
     **/
    attribute long id;

    /**
     * registerToolkitEventListener:
     * @listener: an #EventListener object which will receive the requested
     *            events from the application's toolkits via toolit 'bridges'
     * @eventName: a UTF-8 string indicating the type of (toolkit-specific) event
     *            being requested.  Not all applications can generate toolkit events of
     *            a given type.
     *
     * Register with this application's toolkit for "toolkit-specific" event notifications.
     *       For most event support, clients should use non-toolkit-specific events
     *       whenever possible, via #Registry::registerGlobalEventListener - this method 
     *       is provided as a 'back door' when generic names do not exist for the events in question.
     * SUBJECT TO DEPRECATION in favor of Registry::registerGlobalEventListener forms.
     *
     **/
    void registerToolkitEventListener (in EventListener listener, in string eventName);

    /**
     * registerObjectEventListener:
     * @listener: an #EventListener object which will receive the requested
     *            events
     * @eventName: a UTF-8 string indicating the type of (toolkit-specific) event
     *            being requested.  
     * Register with this application toolkit for "Accessibility::Accessible" 
     * event notifications. SUBJECT TO DEPRECATION
     *
     **/
    void registerObjectEventListener (in EventListener listener, in string eventName);

    /**
     * pause:
     *
     * Request that the application temporarily stop sending events.
     * In most cases this should pause the application's main event loop.
     *
     * Returns: %true if the request succeeded, %false otherwise.
     **/
    boolean       pause ();

    /**
     * resume:
     *
     * Request that the application resume sending events.
     *
     * Returns: %true if the request succeeded, %false otherwise.
     **/
    boolean       resume ();

    void          unImplemented_ ();
    void          unImplemented2_ ();
    void          unImplemented3_ ();
    void          unImplemented4_ ();
  };
};

#endif
